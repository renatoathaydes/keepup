import groovy.text.SimpleTemplateEngine

import java.util.concurrent.TimeUnit

plugins {
    id 'java'
}

version '1.0-SNAPSHOT'

repositories {
    jcenter()
}

dependencies {
    implementation project(':keepup-core')
}

def newVersion = project.version

if (hasProperty('newVersion')) {
    newVersion = getProperty('newVersion')
    buildDir = file("build/${newVersion}")

}

def image = "${buildDir}/image"
def mods = "${buildDir}/mods"

task deps {
    dependsOn jar
    doLast {
        copy {
            from configurations.runtimeClasspath
            from jar
            into mods
        }
    }
}

task generateJavaSources { task ->
    compileJava.dependsOn task
    doLast {
        def template = file('src/main/Version.java.template')
        def generated = new SimpleTemplateEngine().createTemplate(template).make([VERSION: newVersion])
        file('src/main/java/keepup/examples/trivial/Version.java').withWriter { w ->
            generated.writeTo(w)
        }
    }
}

task jlink(type: Exec) {
    dependsOn deps
    inputs.files(sourceSets*.allSource)
    outputs.dir(image)
    def options = [
            '--module-path', mods,
            '--vm=server', // options: client|server|minimal|all
            '--add-modules', 'com.athaydes.keepup.core,keepup.examples.trivial',
            '--output', image,
            '--launcher', 'trivial_app=keepup.examples.trivial/keepup.examples.trivial.TrivialApp',
            '--strip-debug', '--compress', '2', '--no-header-files', '--no-man-pages']
    def jlink = System.getProperty('java.home') + '/bin/jlink'
    commandLine([jlink] + options)
    doFirst { new File(image).deleteDir() }
}

task packageImage(type: Zip) {
    dependsOn jlink
    from image
    into "trivial-app"
    include '**/*'
    archiveName "trivial-app.zip"
    destinationDir file('build')
}

task buildVersion1(type: Exec) {
    workingDir rootDir
    commandLine './gradlew', ':examples:trivial-example:jlink'
}

task buildVersion2(type: Exec) {
    workingDir rootDir
    commandLine './gradlew', ':examples:trivial-example:packageImage', '-PnewVersion=2.0'
}

task testUpdate { task ->
    check.dependsOn task
    dependsOn buildVersion1, buildVersion2
    def launcher = "$buildDir/image/bin/trivial_app"
    def logFile = file("build/app.log")
    doFirst {
        logFile.delete()
    }
    doLast {
        def proc = launcher.execute(null, projectDir)
        def ok = proc.waitFor(5, TimeUnit.SECONDS)
        if (!ok) {
            proc.destroyForcibly()
            assert ok: 'Process did not terminate within timeout'
        }
        assert proc.exitValue() == 0

        def outputIsExpected = false
        def tries = 5
        while (!outputIsExpected && tries > 0) {
            outputIsExpected = logFile.text ==
                    'TrivialApp version 1.0-SNAPSHOT\n' +
                    'TrivialApp version 2.0\n'
            tries--
            sleep 500
        }
        assert tries > 0: 'Log file is not as expected: ' + logFile.text
    }
}
