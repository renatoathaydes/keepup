plugins {
    id 'maven'
    id "com.jfrog.bintray" version "1.8.4"
}

description 'GitHub Distributor for Keepup'

dependencies {
    implementation project(':keepup-core')
    implementation 'org.json:json:20190722'
}

task sourcesJar(type: Jar, dependsOn: classes) {
    from sourceSets.main.allSource
    classifier "sources"
    extension "jar"
}

task javadocJar(type: Jar) {
    classifier "javadoc"
    from javadoc
}

/* Publishing config */

artifacts {
    archives javadocJar, sourcesJar
}

// add all the info required by Maven Central to the pom
configure(install.repositories.mavenInstaller) {
    pom.project {
        inceptionYear '2020'
        name project.name
        packaging 'jar'
        description project.description

        url 'https://github.com/renatoathaydes/keepup'

        scm {
            connection 'git@github.com:renatoathaydes/keepup.git'
            developerConnection 'git@github.com:renatoathaydes/keepup.git'
            url 'https://github.com/renatoathaydes/keepup'
        }

        licenses {
            license {
                name 'The Apache License, Version 2.0'
                url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
            }
        }

        developers {
            developer {
                id 'renatoathaydes'
                name 'Renato Athaydes'
                email 'renato@athaydes.com'
            }
        }
    }
}

bintray {
    user = project.hasProperty("bintrayUserName") ? bintrayUserName : null
    key = project.hasProperty("bintrayApiKey") ? bintrayApiKey : null
    configurations = ['archives']
    publish = true
    pkg {
        repo = 'maven'
        name = 'keepup-github'
        licenses = ['Apache-2.0']
        desc = project.description
        vcsUrl = 'https://github.com/renatoathaydes/keepup.git'
        websiteUrl = 'https://github.com/renatoathaydes/keepup'
        issueTrackerUrl = 'https://github.com/renatoathaydes/keepup/issues'
        labels = ['java', 'javafx', 'self-update', 'github-releases']
        publicDownloadNumbers = true

        //noinspection GroovyAssignabilityCheck
        version {
            name = project.version
            vcsTag = project.version
            gpg {
                sign = true
            }
            mavenCentralSync {
                sync = true
                user = project.hasProperty("ossrhUsername") ? ossrhUsername : null
                password = project.hasProperty("ossrhPassword") ? ossrhPassword : null
                close = '1' // '0' to NOT close
            }
        }
    }

}

bintrayUpload.dependsOn build, sourcesJar
